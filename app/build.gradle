apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.3"
  defaultConfig {
    applicationId "com.trainer"
    minSdkVersion 15
    targetSdkVersion 25
    versionCode 4
    versionName "1.1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    buildConfigField 'String', 'REL_PATH_TO_PLANS', '"/TrainerApp/training_plans/"'
    buildConfigField 'String', 'PLAN_FILE_NAME_TEMPLATE', '"%s.pln"'
    buildConfigField "boolean", "IS_STAGE", 'false'
    buildConfigField "boolean", "IS_PRELIVE", 'false'
    buildConfigField "boolean", "IS_LIVE", 'false'

    resValue("string", "PORT_NUMBER", "8081")   // For debug database
    project.ext.set("appName", "Trainer")
  }

  signingConfigs {
    releasekey {
      storeFile file("../trainer.keystore")
      storePassword "L7!uG)8yQXWG7]=E"
      keyPassword "x4X+*(3y6XxuswtO"
      keyAlias "trainer_release"
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.releasekey
      debuggable false
    }

    debug {
      versionNameSuffix "-DEBUG"
    }
  }

  productFlavors {
    stage {   // Uses mock server
      buildConfigField "boolean", "IS_STAGE", 'true'
      buildConfigField 'String', 'SERVER_ENDPOINT', "\"http://${getIP()}:8080\""
    }

    live {    // Uses real deployed server
      buildConfigField "boolean", "IS_LIVE", 'true'
      buildConfigField 'String', 'SERVER_ENDPOINT', '"http://dummy-live-server.com"'      // TODO: Fill this when server is deployed
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
  }

  // Set APK name
  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      output.outputFile = new File(output.outputFile.parent, "${appName}-${variant.versionName}.apk")
    }
  }
}

kapt { generateStubs = true }

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // Google
  def support_lib_version = '25.3.1'
  compile "com.android.support:appcompat-v7:$support_lib_version"
  compile "com.android.support:support-annotations:$support_lib_version"
  compile "com.android.support:recyclerview-v7:$support_lib_version"
  compile "com.android.support:gridlayout-v7:$support_lib_version"
  compile "com.android.support:cardview-v7:$support_lib_version"

  // Architecture components
  def arch_components_version = '1.0.0-alpha4'
  compile "android.arch.lifecycle:extensions:$arch_components_version"
  kapt "android.arch.lifecycle:compiler:$arch_components_version"
  compile "android.arch.persistence.room:runtime:$arch_components_version"
  compile "android.arch.persistence.room:rxjava2:$arch_components_version"
  kapt "android.arch.persistence.room:compiler:$arch_components_version"

  // Constraint Layout
  compile 'com.android.support.constraint:constraint-layout:1.0.1'

  // PaperParcel
  def paperparcel_version = "2.0.1"
  compile "nz.bradcampbell:paperparcel:$paperparcel_version"
  compile "nz.bradcampbell:paperparcel-kotlin:$paperparcel_version"
  kapt "nz.bradcampbell:paperparcel-compiler:$paperparcel_version"

  // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // Dagger 2
  def dagger_version = '2.8'
  compile "com.google.dagger:dagger:$dagger_version"
  kapt "com.google.dagger:dagger-compiler:$dagger_version"

  // RxJava 2
  compile "io.reactivex.rxjava2:rxjava:2.0.5"
  compile "io.reactivex.rxjava2:rxandroid:2.0.1"

  // Retrofit 2
  def retrofit_version = '2.3.0'
  compile "com.squareup.retrofit2:retrofit:$retrofit_version"
  compile "com.squareup.retrofit2:converter-jackson:$retrofit_version"
  compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

  // OkHttp 3
  def okhttp_version = "3.8.1"
  compile "com.squareup.okhttp3:okhttp:$okhttp_version"
  compile "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"
  compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

  // Picasso
  compile "com.squareup.picasso:picasso:2.5.2"

  // Cupboard
  compile "nl.qbusict:cupboard:2.2.0"

  // Threeten
  compile "com.jakewharton.threetenabp:threetenabp:1.0.4"

  // Jackson
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.9"

  // Circle progress view
  compile 'com.github.HotBitmapGG:RingProgressBar:V1.2.2'

  // Form validation
  compile 'eu.inmite.android.lib:android-validation-komensky:0.9.4@aar'

  // File picker
  compile 'com.github.angads25:filepicker:1.0.6'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // Debug database
  debugCompile 'com.amitshekhar.android:debug-db:1.0.1'

  // TESTING
  testCompile "junit:junit:4.12"
  testCompile 'org.mockito:mockito-core:2.7.0'
  androidTestCompile 'org.mockito:mockito-android:2.6.3'

}
repositories {
  mavenCentral()
}
